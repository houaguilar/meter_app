buildscript {
    ext.kotlin_version = '2.1.0'
}

plugins {
    id "com.android.application"
    // START: FlutterFire Configuration
    id 'com.google.gms.google-services'
    // END: FlutterFire Configuration
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode")
if (flutterVersionCode == null) {
    flutterVersionCode = "1"
}

def flutterVersionName = localProperties.getProperty("flutter.versionName")
if (flutterVersionName == null) {
    flutterVersionName = "1.0"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.mts.metrashop"
    compileSdk = 35
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11

        coreLibraryDesugaringEnabled true

    }

    kotlinOptions {
        jvmTarget = '11'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "com.mts.metrashop"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdk = 21
        targetSdk = 35
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            // Soporta ruta absoluta o relativa (para CI/CD)
            def storeFilePath = keystoreProperties['storeFile']
            if (storeFilePath != null) {
                def storeFileObj = file(storeFilePath)
                if (!storeFileObj.exists()) {
                    // Si no existe como ruta absoluta, buscar en el directorio app/
                    storeFileObj = file("${projectDir}/${storeFilePath}")
                }
                storeFile storeFileObj.exists() ? storeFileObj : null
            }
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig = signingConfigs.debug
            shrinkResources false
            minifyEnabled false
        }
        release {
            signingConfig = signingConfigs.release
            // Habilitar optimizaciones para producción
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source = "../.."
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'org.jetbrains.kotlin') {
            details.useVersion '2.1.0' // Alinea la versión de Kotlin con la que has configurado en otros lugares
        }

        if (details.requested.group == 'com.google.android.gms' && details.requested.name == 'play-services-location') {
            details.useVersion '21.0.0'
        }
    }
}


dependencies {
    implementation 'androidx.multidex:multidex:2.0.1' // Or latest version

    implementation 'androidx.appcompat:appcompat:1.7.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.2' // Versión estable
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.0"

    implementation 'com.google.android.gms:play-services-location:21.0.0'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.gms:play-services-auth:20.5.0'
    implementation 'com.google.android.gms:play-services-base:18.1.0'
    implementation 'com.google.android.gms:play-services-basement:18.1.0'
    implementation 'com.google.android.gms:play-services-tasks:18.2.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "11"
    }
}